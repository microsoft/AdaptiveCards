apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++14 -fexceptions -frtti -DNO_LOCALE_SUPPORT"
                arguments "-DANDROID_STL=c++_shared"
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives")}
    sign configurations.archives
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

def projectInfo = {
    name = 'Android Adaptive Cards Library'
    description = 'Android Adaptive Cards Lib'
    url = 'https://github.com/Microsoft/AdaptiveCards'

    scm {
        url = 'https://github.com/Microsoft/AdaptiveCards'
    }

    licenses {
        license {
            name = 'MIT'
            url = 'https://github.com/Microsoft/AdaptiveCards/blob/main/LICENSE'
            distribution = 'repo'
        }
    }

    developers {
        developer {
            id = "microsoft"
            name = "adaptivecards"
        }
    }

    organization {
        name = 'Microsoft'
        url = 'https://microsoft.com/'
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'io.adaptivecards'
                artifactId = 'adaptivecards-android'

                // Use pre-release identifier, if provided
                def versionSuffix = ''
                if(System.getenv('PRERELEASE_TAG')) {
                    versionSuffix += "-${System.getenv('PRERELEASE_TAG')}"
                    // Append pipeline build number to distinguish pre-releases
                    if(System.getenv('BUILD_BUILDNUMBER')) {
                        versionSuffix += ".${System.getenv('BUILD_BUILDNUMBER')}"
                    }
                }
                version = "${acVersionMajor}.${acVersionMinor}.${acVersionPatch}${versionSuffix}"

                pom projectInfo
            }

        }

        repositories {
            // Publish to Azure Artifacts feed
            maven {
                name = 'azuredevops'
                url = 'https://microsoft.pkgs.visualstudio.com/_packaging/AdaptiveCards/maven/v1'
                credentials {
                    username "AZURE_ARTIFACTS"
                    password System.getenv("AZURE_ARTIFACTS_ENV_ACCESS_TOKEN") ?: "${azureArtifactsGradleAccessToken}"
                }
            }
            // Save to local directory "artifact"
            maven {
                name = 'localDirectory'
                url = "artifact"
            }
            // TODO: Add SonaType OSS repository to publish directly
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:2.0.1'
    implementation 'com.google.android:flexbox:2.0.1'
    testImplementation 'junit:junit:4.12'
}
